//@compile{true} 

//@include{CocoClip.jspp}
//@include{WebGL.js}
//@include{glu.js}

/* ***** BEGIN LICENSE BLOCK *****
 *
 * Copyright (C) 2013 www.coconut2D.org
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.   
 *
 * ***** END LICENSE BLOCK ***** */

// ==================================================================================================================================
//	   ______                _____                    
//	  / ____/___  _________ / ___/________  ____  ___ 
//	 / /   / __ \/ ___/ __ \\__ \/ ___/ _ \/ __ \/ _ \
//	/ /___/ /_/ / /__/ /_/ /__/ / /__/  __/ / / /  __/
//	\____/\____/\___/\____/____/\___/\___/_/ /_/\___/ 
//	                                                  
// ==================================================================================================================================
  
class CocoScene
{
	// Resources 
	public var __sceneName:String 				= "";   	
	public var __imageSymbols:Object 			= {};   	
	public var __soundSymbols:Object 			= {};   	
	public var __clipSymbols:Object 			= {}; 
	
	// Fixed View Port  	
	public var __view_width:Number				= 0;
	public var __view_height:Number				= 0;
	public var __view_pixel_ratio:Number		= 0;	
	public var __view_scale:Number				= 1;

	// Rendering
	public var __currentTime:Number 			= 0.0;  	
	public var __elapsedTime:Number 			= 0.0;  	
	public var __startTime:Number 				= -1;   	
	public var __root:CocoClip 					= null;
	public var __modelViewMatrix:CocoMatrix		= null;
	public var __projectionMatrix:CocoMatrix 	= null;
	public var __glProgram:WebGLProgram			= null;
	public var __levelParents:Array				= [];
	
	public var __vertex_shader:String			= "attribute vec2 iVecCoords;attribute vec2 iTexCoords;uniform mat4 uProjMat;uniform mat4 uMVMat;uniform vec2 uSprSize;uniform vec2 uSprFrame;varying vec2 pTexCoord;void main(void){ gl_Position = uProjMat * uMVMat * vec4(iVecCoords, 0.0, 1.0); pTexCoord = vec2(((iTexCoords.x + uSprFrame.x) * uSprSize.x), ((iTexCoords.y + uSprFrame.y) * uSprSize.y));}";
	public var __fragment_shader:String			= "#ifdef GL_ES\nprecision mediump float;\n#endif\nuniform sampler2D uSampler;uniform vec4 uColor;varying vec2 pTexCoord;void main(void){ gl_FragColor = vec4(texture2D(uSampler, pTexCoord).r * uColor.r, texture2D(uSampler, pTexCoord).g * uColor.g, texture2D(uSampler, pTexCoord).b * uColor.b, texture2D(uSampler, pTexCoord).a * uColor.a);}";
	
	public var __boundingBoxVertexShader:String = "attribute vec2 iVec2Coords;uniform mat4 uProjMat;void main(void) { gl_Position = uProjMat * vec4(iVec2Coords, 0.0, 1.0);}";
	public var __boundingBoxFragmentShader:String	= "#ifdef GL_ES\nprecision mediump float;\n#endif\nvoid main(void) { gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);}";
	public var __boundingBoxProgram:WebGLProgram	= null;
	public var __boundingBoxBuffer:WebGLBuffer	= null;
	
	public var __glPayload:Array = [];

			            
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function Constructor(sceneName:String)
	{ 
		__sceneName			= sceneName;
		__root 				= new CocoClip();
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function updateModelViewMatrix(gl:WebGLRenderingContext) 
	{
		__modelViewMatrix.update(gl, __glProgram.GLSLuMVMat); 
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function updateProjectionMatrix(gl:WebGLRenderingContext) 
	{
		__projectionMatrix.update(gl, __glProgram.GLSLuProjMat); 
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function resourceImagesCount() :Number
	{
		var t:String, c:Number = 0;
	    for(t in __imageSymbols) 
			if(__imageSymbols[t])
				c++;
	    return c;
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function resourceImagesLoaded() :Boolean
	{
	    var t:String, img:CocoImage;	    
	    for(t in __imageSymbols) 
	    {
	    	img = __imageSymbols[t].image;
	    	if(!img.width || !img.height) 
	    		return false;
	    }
	    return true;
	} 
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function getImageSymbol(symbolName:String) :CocoImage
	{
	    var img:CocoImage = __imageSymbols[symbolName];
	    if(img) 
	    {
	        var dpi:SCREEN_RESOLUTION_DPI_ENUM = SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_nodpi;
	        switch(window.devicePixelRatio)
	        {
	        case 0.75: dpi = SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_ldpi; break;
	        case 1.00: dpi = SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_mdpi; break;
	        case 1.25: dpi = SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_tvdpi; break;
	        case 1.50: dpi = SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_hdpi; break;
	        case 2.00: dpi = SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_xhdpi; break;
	        default: return img;
	        }
	        return img.getDpiSiblingImage(dpi);
	    }
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function getSoundSymbol(symbolName:String) :CocoSound
	{
		return __soundSymbols[symbolName] || null;
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function getClipSymbol(symbolName:String) :CocoClip
	{
		return __clipSymbols[symbolName] || null;
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function newResourceImage(symbolName:String, filename:String) :CocoImage
	{
	    if(__imageSymbols[symbolName]) throw "Resource " + symbolName + " already exists.";
		var img:CocoImage = new CocoImage();
		img.symbolName = symbolName;
		img.createFromFile(filename, null);		
		__imageSymbols[symbolName] = img;
		return img;
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function newResourceSound(symbolName:String, filename:String) :CocoSound
	{
	    if(__soundSymbols[symbolName]) throw "Resource " + symbolName + " already exists.";	    	
	    var snd:CocoSound = new CocoSound();
	    snd.symbolName = symbolName;	    
	    snd.createFromFile(filename, null); 
	    __soundSymbols[symbolName] = snd;
	    return snd;
	}
		
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function prepared() :Boolean		
	{  
		return (__glProgram!=null); 
	}
		
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function prepare(gl:WebGLRenderingContext)
	{                                                                         		                    
		// Disable depth testing since we render using scene's clips order.		                    
		gl.disable(gl.DEPTH_TEST);
		
		// Enable alpha blending.
		gl.enable(gl.BLEND);     
		gl.blendFunc(gl.SRC_ALPHA, gl.ONE_MINUS_SRC_ALPHA);
		
		// Disable culling.
		gl.disable(gl.CULL_FACE);
		
		__boundingBoxProgram = glu.makeProgram(gl, __boundingBoxVertexShader, __boundingBoxFragmentShader); 
		
		gl.useProgram(__boundingBoxProgram);
		__boundingBoxProgram.GLSLiVec2Coords	= gl.getAttribLocation(__boundingBoxProgram, "iVec2Coords");
		__boundingBoxProgram.GLSLuProjMat		= gl.getUniformLocation(__boundingBoxProgram, "uProjMat");
		__boundingBoxBuffer	= gl.createBuffer();
		gl.bindBuffer(gl.ARRAY_BUFFER, __boundingBoxBuffer);
		gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0]), gl.DYNAMIC_DRAW);
		
		__glProgram = glu.makeProgram(gl, __vertex_shader, __fragment_shader);
		
		gl.useProgram(__glProgram);
		
		__glProgram.GLSLiVecCoords 	= gl.getAttribLocation(__glProgram, "iVecCoords");
		__glProgram.GLSLiTexCoords 	= gl.getAttribLocation(__glProgram, "iTexCoords");
		__glProgram.GLSLuProjMat 	= gl.getUniformLocation(__glProgram, "uProjMat");
		__glProgram.GLSLuMVMat 		= gl.getUniformLocation(__glProgram, "uMVMat");
		__glProgram.GLSLuSprSize 	= gl.getUniformLocation(__glProgram, "uSprSize");
		__glProgram.GLSLuSprFrame 	= gl.getUniformLocation(__glProgram, "uSprFrame");
		__glProgram.GLSLuSampler 	= gl.getUniformLocation(__glProgram, "uSampler");
		__glProgram.GLSLuColor 		= gl.getUniformLocation(__glProgram, "uColor");
	
		
		__modelViewMatrix = new CocoMatrix();
		__modelViewMatrix.update(gl, __glProgram.GLSLuMVMat);
			
		// If we have a fixed view, we scale to it, otherwise we use device pixel ratio.
		__view_scale = this.__view_width>0 && this.__view_height>0 ? Math.min(gl.canvas.width / this.__view_width, gl.canvas.height / this.__view_height) : window.devicePixelRatio;
		
		__projectionMatrix = new CocoMatrix();
		__projectionMatrix.ortho(-gl.canvas.width / 2.0, gl.canvas.width / 2.0, gl.canvas.height / 2.0, -gl.canvas.height / 2.0, -1.0, 1.0);				
		
		if(window.deviceRotation) __projectionMatrix.rotateZ(-window.deviceRotation);
		
		__projectionMatrix.scale(__view_scale, __view_scale);
		__projectionMatrix.update(gl, __glProgram.GLSLuProjMat);
					
		gl.enableVertexAttribArray(__glProgram.GLSLiTexCoords);
		gl.enableVertexAttribArray(__glProgram.GLSLiVecCoords);		    

		var t:String;
	    for(t in __imageSymbols)    
	    {
	    	try 
	    	{
	    		__imageSymbols[t].prepare(gl);
	    	}
	    	catch(e)
	    	{
	    		alert("Image " + t + " failed to prepare texture");
	    	}
	    }
	    	
	    if(gl.getError())  
	    {
	    	alert("WebGL Error " + gl.getError());
	    } 
	    
	    trace("Scene [" + this.__sceneName + "] loaded and prepared.");
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function tick(gl:WebGLRenderingContext, time:Number)
	{
		render(gl, time);		
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function render(gl:WebGLRenderingContext, time:Number, picking:Boolean)
	{
		__modelViewMatrix.identity();
		if(__startTime == -1) __startTime = time;
	    __elapsedTime = time - __currentTime - __startTime;
	    __currentTime = time - __startTime;
					    
	    if(__root)    
	    {
	    	// Render the scene's root symbol
	    	__levelParents = [__root];
	    	{
				// Offscreen rendering
				
				//if(picking)
				//{
				//	gl.bindFramebuffer(gl.FRAMEBUFFER, framebuffer);
				//	gl.uniform1i(__glProgram.uOffscreen, true);
				//	__root.render(gl, this, null, true);
				//}
				//
				//// On screen rendering
				//gl.bindFramebuffer(gl.FRAMEBUFFER, null);
				//gl.uniform1i(__glProgram.uOffscreen, false);
				
				if(global.__Coconut2DPlayer)
				{
                	__glPayload = [];
					__root.render(gl, this, null, false);	                
                	__WebGLRenderingContext_execute(__glPayload);
				}
				else
				{
					__root.render(gl, this, null, false);	
				}
	    	}				
			__levelParents = [];
			
			// Did we reach the end of the main timeline?
			var max = __root.childWithMaxTimelineDuration();
			if(max && max.__currentFrame == max.__timeline.__lastKeyFrame)
			{ 
				//__root.reset();			
				//__startTime = -1;	
			} 
	    }	    
	} 
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function drawFrame(gl:WebGLRenderingContext, image:CocoImage, frame:Number, alpha:Number)
	{
		image.color[3] = alpha;		
		
		if(global.__Coconut2DPlayer)
		{			
			__glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.UNIFORM2F, __glProgram.GLSLuSprSize.__uid, image.texSize[0], image.texSize[1]]);	        
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.UNIFORM2F, __glProgram.GLSLuSprFrame.__uid, image.textureGrid[frame * 2 + 1], image.textureGrid[frame * 2]]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.BINDBUFFER, gl.ARRAY_BUFFER, image.buffer.__uid]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.VERTEXATTRIBPOINTER, __glProgram.GLSLiTexCoords.__uid, 2, gl.FLOAT, false, 4 * 4, 0]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.VERTEXATTRIBPOINTER, __glProgram.GLSLiVecCoords.__uid, 2, gl.FLOAT, false, 4 * 4, 2 * 4]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.BINDTEXTURE, gl.TEXTURE_2D, image.texture.__uid]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.UNIFORM1I, __glProgram.GLSLuSampler.__uid, 0]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.UNIFORM4F, __glProgram.GLSLuColor.__uid, image.color[0], image.color[1], image.color[2], image.color[3]]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.DRAWARRAYS, gl.TRIANGLE_STRIP, 0, 4]);
	        __glPayload = __glPayload.concat([COCO_WEBGL_PAYLOAD_OP.BINDBUFFER, gl.ARRAY_BUFFER, 0, 0]);	        
		}
		else
		{  
			gl.uniform2fv(__glProgram.GLSLuSprSize, image.texSize);
			gl.uniform2f(__glProgram.GLSLuSprFrame, image.textureGrid[frame * 2 + 1], image.textureGrid[frame * 2]);                
			gl.bindBuffer(gl.ARRAY_BUFFER, image.buffer);                
			gl.vertexAttribPointer(__glProgram.GLSLiTexCoords, 2, gl.FLOAT, false, 4 * 4, 0);                
			gl.vertexAttribPointer(__glProgram.GLSLiVecCoords, 2, gl.FLOAT, false, 4 * 4, 2 * 4);
			gl.bindTexture(gl.TEXTURE_2D, image.texture);                
			gl.uniform1i(__glProgram.GLSLuSampler, 0);                
			gl.uniform4fv(__glProgram.GLSLuColor, image.color);
			gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);                 
			gl.bindBuffer(gl.ARRAY_BUFFER, null); 			
		} 		
 	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function hitTest(MouseX:Number, MouseY:Number) :Array
	{
		
	}
			
	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function gotoAndPlay(LabelNameOrFrameIndex:Object, deep:Boolean) :Boolean
	{  
		var scope:CocoClip = __levelParents[__levelParents.length-1];
		return scope.gotoFrame(LabelNameOrFrameIndex, false, deep);
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function gotoAndStop(LabelNameOrFrameIndex:Object, deep:Boolean) :Boolean
	{  	 
		var scope:CocoClip = __levelParents[__levelParents.length-1];
		return scope.gotoFrame(LabelNameOrFrameIndex, true, deep);
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function stop(deep:Boolean) :Boolean
	{
		deep = (deep=="true");
		var scope:CocoClip = __levelParents[__levelParents.length-1];		
		return scope.gotoFrame(-1, true, deep);
	}	
}






