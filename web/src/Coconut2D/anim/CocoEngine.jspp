"#export native"
"#export web"
/* ***** BEGIN LICENSE BLOCK *****
 *
 * Copyright (C) 2013-2014 www.coconut2D.org
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 * ***** END LICENSE BLOCK ***** */

// ==================================================================================================================================
//	   ______                 ______            _
//	  / ____/___  _________  / ____/___  ____ _(_)___  ___
//	 / /   / __ \/ ___/ __ \/ __/ / __ \/ __ `/ / __ \/ _ \
//	/ /___/ /_/ / /__/ /_/ / /___/ / / / /_/ / / / / /  __/
//	\____/\____/\___/\____/_____/_/ /_/\__, /_/_/ /_/\___/
//	                                  /____/
// ==================================================================================================================================

"#include WebGLRenderingContext.jspp"
"#include CocoScene.jspp"
"#include DeviceEvent.jspp"

state class CocoEngine
{
	public const TICK_TIME:Float = (1000.0/60.0);

	private var __currentState:State;
	private var __nextState:State;
	private var __deviceEvent:DeviceEvent;
	private var __clicked:Array<CocoClip>;
	private var __clock:Float;

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function Constructor()
	{
		// Set event handlers
		__clicked = [];
		__deviceEvent = null;
		__clock = 0;

		// Set event handlers
		window.addEventListener("touchstart", __setTouchEvent);
		window.addEventListener("touchmove", __setTouchEvent);
		window.addEventListener("touchend", __setTouchEvent);
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function Destructor()
	{
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function setNextState(s:State)
	{
		__nextState = s;
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function run(gl:WebGLRenderingContext, time:Float)
	{
		if(!__currentState && !__nextState) return;

		if(__nextState!=null)
		{
			if(__currentState) __currentState.exit();
			__currentState = __nextState;
			__currentState.enter();
			__nextState = null;
			__clicked = [];
			trace("@@STATE " + __currentState.__name);
		}

		__currentState.tick(__clock);
		__clicked = [];

		if(__nextState==null)
		{
			gl.clearColor(0,0,0,1);
			gl.clear(gl.COLOR_BUFFER_BIT);
			__currentState.paint(gl, __clock);
		}

		__deviceEvent = null;
		__clock += TICK_TIME;
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	private function __setTouchEvent(e:DeviceEvent)
	{
		__deviceEvent = e;
		__deviceEvent.__clientX = e.touches[0].clientX;
		__deviceEvent.__clientY = e.touches[0].clientY;
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function getTouchEvent() :DeviceEvent
	{
		// In the future we need to filter the event type
		return __deviceEvent;
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function pushTouched(clip:CocoClip)
	{
		trace("click: " + clip.__instanceName);
		__clicked.push(clip);
	}

	///////////////////////////////////////////////////////////////////////////////////////////////
	public function isClicked(check:Array<CocoClip>):Integer
	{
		for(var i:Integer = __clicked.size()-1; i>=0; i--)
			for(var r:Integer = check.size()-1; r>=0; r--)
				if(check[r] == __clicked[i])
					return r;
		return -1;
	}

}
