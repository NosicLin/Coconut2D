/* ***** BEGIN LICENSE BLOCK *****
 *
 * Copyright (C) 2013-2014 www.coconut2D.org
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 * ***** END LICENSE BLOCK ***** */

#pragma export_native;
#pragma export_web;

#include "CanvasRenderingContext2D.jspp"

// ==================================================================================================================================
//	   ______                 ____            __
//	  / ____/___  _________  / __ \___  _____/ /_
//	 / /   / __ \/ ___/ __ \/ /_/ / _ \/ ___/ __/
//	/ /___/ /_/ / /__/ /_/ / _, _/  __/ /__/ /_
//	\____/\____/\___/\____/_/ |_|\___/\___/\__/
//
// ==================================================================================================================================

struct CocoPoint
{
	x:Float;
	y:Float;
}

struct CocoRect
{
	left:Float;
	top:Float;
	right:Float;
	bottom:Float;
}

struct CocoSkinCacheItem
{
	symbolName:String;
	width:Integer;
	height:Integer;
	image:CocoImage;
}

struct CocoHVAlign
{
	horiz:Float;
	vert:Float;
}

// ==================================================================================================================================
//	   ______                 ______                 __    _
//	  / ____/___  _________  / ____/________ _____  / /_  (_)_________
//	 / /   / __ \/ ___/ __ \/ / __/ ___/ __ `/ __ \/ __ \/ / ___/ ___/
//	/ /___/ /_/ / /__/ /_/ / /_/ / /  / /_/ / /_/ / / / / / /__(__  )
//	\____/\____/\___/\____/\____/_/   \__,_/ .___/_/ /_/_/\___/____/
//	                                      /_/
// ==================================================================================================================================

class CocoGraphics
{
	public static var skinsCache:Array<CocoSkinCacheItem>;

 	public function Constructor()
 	{
 	}

 	public function Destructor()
 	{
 	}

	/////////////////////////////////////////////////////////////////////////////////////
 	public static function POT(n:Integer):Integer
 	{
 		return Math.pow(2, Math.ceil(Math.log(n)/Math.log(2)));
 	}

	// ==================================================================================================================================
	//	   ______                               ______                 __  _
	//	  / ____/___ _____ _   ______ ______   / ____/_  ______  _____/ /_(_)___  ____  _____
	//	 / /   / __ `/ __ \ | / / __ `/ ___/  / /_  / / / / __ \/ ___/ __/ / __ \/ __ \/ ___/
	//	/ /___/ /_/ / / / / |/ / /_/ (__  )  / __/ / /_/ / / / / /__/ /_/ / /_/ / / / (__  )
	//	\____/\__,_/_/ /_/|___/\__,_/____/  /_/    \__,_/_/ /_/\___/\__/_/\____/_/ /_/____/
	//
	// ==================================================================================================================================

	public static function NewCanvas(width:Integer, height:Integer, pot:Boolean) :HTMLCanvasElement
	{
    	var canvas:HTMLCanvasElement = HTMLCanvasElement(document.createElement("canvas"));
    	width = Math.max(1, width);
    	height = Math.max(1, height);
		canvas.width = Math.max(1, (pot ? POT(width) : width));
		canvas.height = Math.max(1, (pot ? POT(height) : height));
		return canvas;
	}

	// ==================================================================================================================================
	//	   ______      __              ______                 __  _
	//	  / ____/___  / /___  _____   / ____/_  ______  _____/ /_(_)___  ____  _____
	//	 / /   / __ \/ / __ \/ ___/  / /_  / / / / __ \/ ___/ __/ / __ \/ __ \/ ___/
	//	/ /___/ /_/ / / /_/ / /     / __/ / /_/ / / / / /__/ /_/ / /_/ / / / (__  )
	//	\____/\____/_/\____/_/     /_/    \__,_/_/ /_/\___/\__/_/\____/_/ /_/____/
	//
	// ==================================================================================================================================

	public static function ColorToHtmlRGBA(c:Color) :String
	{
		try
		{
			var A:Integer = ((c >> 24) & 0xFF);
			var R:Integer = ((c >> 16) & 0xFF);
			var G:Integer = ((c >>  8) & 0xFF);
			var B:Integer = ((c)       & 0xFF);

			return "rgba(" + String(R) + "," + String(G) + "," + String(B) + "," + String(A/255.0) + ")";
		}
		catch(e:Error)
		{
			return "rgba(0,0,0,0)";
		}
	}

	// ==================================================================================================================================
	//	    ____  ____  ____   ______                 __  _
	//	   / __ \/ __ \/  _/  / ____/_  ______  _____/ /_(_)___  ____  _____
	//	  / / / / /_/ // /   / /_  / / / / __ \/ ___/ __/ / __ \/ __ \/ ___/
	//	 / /_/ / ____// /   / __/ / /_/ / / / / /__/ /_/ / /_/ / / / (__  )
	//	/_____/_/   /___/  /_/    \__,_/_/ /_/\___/\__/_/\____/_/ /_/____/
	//
	// ==================================================================================================================================

	public static function DpiToPixelRatio(dpi:SCREEN_RESOLUTION_DPI_ENUM) :Float
	{
		switch(dpi)
		{
		case SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_ldpi:	return 0.75;
		case SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_mdpi:	return 1.00;
		case SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_tvdpi:	return 1.25;
		case SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_hdpi:	return 1.50;
		case SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_xhdpi:	return 2.00;
		default:
			return SCREEN_RESOLUTION_DPI_ENUM.RESOLUTION_nodpi;
		}
	}

	// ==================================================================================================================================
	//	   _____ __   _          ______                 __  _
	//	  / ___// /__(_)___     / ____/_  ______  _____/ /_(_)___  ____  _____
	//	  \__ \/ //_/ / __ \   / /_  / / / / __ \/ ___/ __/ / __ \/ __ \/ ___/
	//	 ___/ / ,< / / / / /  / __/ / /_/ / / / / /__/ /_/ / /_/ / / / (__  )
	//	/____/_/|_/_/_/ /_/  /_/    \__,_/_/ /_/\___/\__/_/\____/_/ /_/____/
	//
	// ==================================================================================================================================

	public static function GetSkinFromCache(skinImage:CocoImage, width:Integer, height:Integer) :CocoImage
	{
		if(!skinImage || width==0 || height==0)
			return null;

		// Initialize skin cache array
		if(!skinsCache)
			skinsCache = new Array<CocoSkinCacheItem>;

		// Search cache for skin
		for(var i:Integer = skinsCache.size()-1; i>=0; i--)
		{
			var img:CocoSkinCacheItem = skinsCache[i];
			if(img.symbolName==skinImage.symbolName && img.width==width && img.height==height)
				return img.image;
		}

		// Create a skin image
		var w:Integer = Math.floor(width*skinImage.pixelRatio);
		var h:Integer = Math.floor(height*skinImage.pixelRatio);
		var canvas:HTMLCanvasElement = NewCanvas(w, h, true);
		var ctx:CanvasRenderingContext2D = CanvasRenderingContext2D(canvas.getContext("2d"));
		CocoGraphics.DrawSkin(ctx, skinImage, 0, 0, w, h);
		ctx = null;
		var image:CocoImage = CocoImageFromHTMLCanvas(canvas, w, h);
		image.pixelRatio = skinImage.pixelRatio;
		delete canvas;

		// Save skin in cache
		var item:CocoSkinCacheItem = new CocoSkinCacheItem();
		item.symbolName = skinImage.symbolName;
		item.width = width;
		item.height = height;
		item.image = image;
		skinsCache.push(item);

		return image;
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public static function CocoImageFromHTMLCanvas(canvas:HTMLCanvasElement, textureWidth:Integer, textureHeight:Integer, optional mime:String = "image/png") :CocoImage
	{
        var img:CocoImage = new CocoImage();
        img.hImage = new Image();
        img.textureCellWidth = textureWidth;
        img.textureCellHeight = textureHeight;
        img.textureGrid = new Float32Array([0,0]);
        if(mime=="") mime = "image/png";
        try
        {
        	var data:String = canvas.toDataURL(mime);
        	img.hImage.load(data);
        }
        catch(e:Error)
        {
        	var empty:String = "data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAIAAAACACAMAAAD04JH5AAAAGXRFWHRTb2Z0d2FyZQBBZG9iZSBJbWFnZVJlYWR5ccllPAAAADBQTFRFyNDhwsvb/f79v8fV5+zz2N3n7PL44ebu9/v+8fb79PP1/Pv6+Pf67u/w////xMzfUtD/OgAAAwFjb2NvOm1ldGGTsgAA/AIAAAgACACvV0byRAAAAG4AbwBkAGUAbgBhAG0AZQARAAAARgBhAGMAZQBiAG8AbwBrAF8ATgBvAF8ASQBtAGEAZwBlAAAAAwAKAI1Psk4kAAAAbwB1AHQAcAB1AHQAdAB5AHAAZQAAAAAAAwADAGgADpAYAAAAZABwAGkAAAAAAAAACAAEAGg2DpAYAAAAbABkAHAAaQAAAAAACAAEAOg2DpAYAAAAbQBkAHAAaQAAAAAACAAFAGg7TpccAAAAdAB2AGQAcABpAAAAAAAAAAgABABoNA6QGAAAAGgAZABwAGkAAAAAAAgABQBoNI6XHAAAAHgAaABkAHAAaQAAAAAAAAADAAIAMeAOABQAAAB3ADEAAAAAAAMAAgAy4A4AFAAAAHcAMgAAAAAAAwACADPgDgAUAAAAdwAzAAAAAAADAAIAMQANABQAAABoADEAAAAAAAMAAgAyAA0AFAAAAGgAMgAAAAAAAwACADMADQAUAAAAaAAzAAAAAAADABAAXaW/6jAAAAB0AGUAeAB0AHUAcgBlAGMAZQBsAGwAdwBpAGQAdABoAAAAAAADABEAnbtruDQAAAB0AGUAeAB0AHUAcgBlAGMAZQBsAGwAaABlAGkAZwBoAHQAAAAAAAAABAAMALlEGyQoAAAAdABlAHgAdAB1AHIAZQBzAGMAYQBsAGUAAACAPwgACwDSKuBiLAAAAHQAZQB4AHQAdQByAGUAZwByAGkAZAADAAAAMAA7ADAAAgAOAMK+vJYsAAAAcwBlAHEAdQBlAG4AYwBlAHMAYwBvAHUAbgB0AAAAAAAIAAwAtZaf0ygAAAB0AGkAbABlAG0AYQBwAGMAZQBsAGwAcwAAAAAAAwATAIVddJg4AAAAdABpAGwAZQBkAGwAYQB5AGUAcgBjAGUAbABsAHcAaQBkAHQAaAAAAAAAAAADABQAxLVwx0T9//90AGkAbABlAGQAbABhAHkAZQByAGMAZQBsAGwAaABlAGkAZwBoAHQAAAAAABg19usAAAYQSURBVHja7JsBl+MoCIABYzRNqv//3x5g0nZ2pilmSHv3bn1vt7OzSflEREAF+HSrH21YHQm4N6ifPQCuKoAvH+8GQAwhAH98AABVeED+Nvm5vgkAWndRRALElIZxkN/VdwBoNxGb8JjGy5QLt7FX/kEA7TLomMc4LiqbcqEEnQNwXAMqHiBdVTiLLzmXCdgO6jsAQCRBHBaVTTQTyU9XCM0MTwcQ3aeJhbLowtKlMcgSRStnzQK8ORtRflrK2nvS3s8CUJgAz5qGOsdV/iq+aX1r2z8SqhUwJRuJswbWroUQL6vyf2qDDkNFqzWaAaDpAAMMs4h/Ir+UiQmqWgK4G6FAxJEHfFb5PyHw7zIPg32N7QCQ7teUb8qnZzrIA4gS/AFY/Slvs361/j8V0CYkE7jPArasgAPdJt7P/d88wiDPO09DtqtIq5DytK2KoWSNCuyzAEPM5anuvwLwyhCN/sgOEHCUb1YN7wMoxGQ0A/sQsAJujne3yTNLphTA2QhHsgPMsjajL4BYgEX+bRiSyRtbATCk8sT5/ewPqVx9AfCq9tcDgK4AcCn79v+tLeAKEJdegDn6AlAPgD7sChBi+1ZzOwPALp/W8MwfgP4C/I8B0n9LA+owkrsjIrsjkgfjRwDuITs5A8wvwtFHADoBAJZiWgr+RQDOqyHHA70AXvFASzEkIDHbgGaPC7SE0kMDUhu4lK0UZNIAMUAw5OgGAP3/wDFhma1DwKylXGowFI4tGkDNzIcOG9Anr1pNRAcNoCTGcF07ZraBi+TzPgDih0YxgA4AtoIxsfLQYQikOjSu7tWeF/CfSSq3DgDQAPqbAED10cBwBGA0VE2NAPUQwOAFsAZE3U0iEodZIH4gRDoCEKrTNDwGQNELAGuAqXSlRvJkjkH8ADgMgRRo+lJjgZ1AAFxsgNeiod8RjbIauQDIBlFqezPG0oAG8EPwsYG1SKfVATOALBymKpXNEcmKKAFJlxUu/FL1ckT8TfHaY4VEl2gqF3eUaiORfQ5SiaeUau2TQJ2A64ZF6FuSJ3eAzhXxCt7l+rVYa5yH7ATQe9eMrTAb/QBlqw32lOtDneyuOEMA351T5EWxIyoZ5AXnnVPQGoGxRTxj37AuVvkZ0HsWaGCSOkbAf+tWovPF4gW1OILoD9CKdQu9KJIvEo2aDzF0AuBY8m61kJeLXKZq3jruPUMSIO9n6RIzUbTL7wYI42uACc8DqBqc7gJQufacpuoF4BUpzzv+aJ5zGc48R2QCCHjqECz7u+fZuGN5DECiklxeAIxwIoCkaLsAmpKdB9CSVNr3xPlMP7CeYtjzhHQuAL0C4HYKADykiPRityh2zMMuDWBYk+QXACmcc5KKH44XyzGeSzQUCLuDUnl2yGQBoDyBf0zYTjJpjr4fD7QtO/fzhJqhmwB0WyO5x4QtL6BX5yi2B4ZaK7oPwWDPjEYM/ofZwJyazVoj9E1OIYgTshap1Bc4Z8chdNUHRnQuULACxp5C5RTR7yiX3F+AJAf4bUMgoXGWo63Yv1/Q3ljfQ5Ut5+hjmkpXtVrLRKlpYcP48fLDdwBsj25vcOdxPUbf35ZBEDDg/QttQwA16K6vHJBm8bkcbnnQPFXP/ONPM/OJDazvyA2KoZTeSv3XbYMxyW2E1pvaAYB6h2F8fZL3FUChUY0Bu2aBdr5dItDrA/ORLRvNYPSvnKJe/0ATAFYJaFrn2ynKgxqghzPgNEaZTvelFf8EwNvYM2rUCxyPx7UPDsHDmzQ1NcCXqzg3gPVyis6ZOOZfWN7eNpaqAdeewneAdm1pKu7i7984JUXAb0Ow+qxt0v9C7/uxGqvh+rhM3DVQWfdpzNu9IaJbFv4blNu77U7G3OxyGm4MG4A63GZ486zS77dYDmvj8UX+urmdgVCEvCGg3vpU8V+2BcnfBu5m1X4Yo94WA7W+NvTzXN7UdAOS50S7NadXR57fHDoFgNaBgCDbYVOzufl98ttNJW45sulzsJvbJKH5XQpYKwnEqwRQztvFmXcNwv2mEMsGut/coTfawO0iBJDx6sYplrgClI81ASgfbn8B/hFgAEpLN8V/V8HtAAAAAElFTkSuQmCC";
        	img.hImage.load(empty);
        }
        return img;
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public static function DrawSkin(ctx:CanvasRenderingContext2D, skinImage:CocoImage, x:Integer, y:Integer, width:Integer, height:Integer)
	{
		if(!skinImage)
			return;

		var W1:Integer				  = skinImage.skin[0];
		var W2:Integer				  = skinImage.skin[1];
		var W3:Integer				  = skinImage.skin[2];
		var H1:Integer				  = skinImage.skin[3];
		var H2:Integer				  = skinImage.skin[4];
		var H3:Integer				  = skinImage.skin[5];

		var RW1H1:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE;
		var RW2H1:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_X;
		var RW3H1:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE;
		var RW1H2:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_Y;
		var RW2H2:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_XY;
		var RW3H2:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_Y;
		var RW1H3:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE;
		var RW2H3:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_X;
		var RW3H3:COCO_SKIN_TILE_ENUM = COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE;

		var	F:Integer;
		var	RmW:Integer;
		var	RmH:Integer;
		var	R:Array<Integer> = [0,0,0];
		var	C:Array<Integer> = [0,0,0];

	    // Find slices with fixed width or height
	    if(__fixedH([RW1H1, RW2H1, RW3H1], true)) R[0] = H1;
	    if(__fixedH([RW1H2, RW2H2, RW3H2], true)) R[1] = H2;
	    if(__fixedH([RW1H3, RW2H3, RW3H3], true)) R[2] = H3;
	    if(__fixedW([RW1H1, RW1H2, RW1H3], true)) C[0] = W1;
	    if(__fixedW([RW2H1, RW2H2, RW2H3], true)) C[1] = W2;
	    if(__fixedW([RW3H1, RW3H2, RW3H3], true)) C[2] = W3;

		// Count how many cells have fixed height and divide the remaining height to the others
		F = __countFixed([H1, H2, H3], true) - __countFixed(R);
		RmH = Math.max(0, height - R[0] - R[1] - R[2]);
		if(F && H1!=0 && R[0]==0) R[0] =(RmH / F);
		if(F && H2!=0 && R[1]==0) R[1] =(RmH / F);
		if(F && H3!=0 && R[2]==0) R[2] =(RmH / F);

		// Count how many cells have fixed width and divide the remaining width to the others
		F = __countFixed([W1, W2, W3], true) - __countFixed(C);
		RmW = Math.max(0, width - C[0] - C[1] - C[2]);
		if(F && W1!=0 && C[0]==0) C[0] = (RmW / F);
		if(F && W2!=0 && C[1]==0) C[1] = (RmW / F);
		if(F && W3!=0 && C[2]==0) C[2] = (RmW / F);

	    // Draw slices

	    ctx.save();
	    ctx.setTransform(1,0,0,1,0,0);

	    __drawSlice ( ctx, skinImage, 	x, 					y, 				 	C[0],	R[0], 	0, 			0,			W1, 	H1, 	RW1H1);
	    __drawSlice ( ctx, skinImage, 	x + C[0], 			y, 				 	C[1],	R[0], 	W1, 		0,			W2, 	H1, 	RW2H1);
	    __drawSlice ( ctx, skinImage, 	x + C[0] + C[1], 	y, 				 	C[2],	R[0], 	W1 + W2, 	0,			W3, 	H1, 	RW3H1);
	    __drawSlice ( ctx, skinImage, 	x, 					y + R[0], 		 	C[0],	R[1], 	0, 			H1,			W1, 	H2, 	RW1H2);
	    __drawSlice ( ctx, skinImage, 	x + C[0], 			y + R[0], 		 	C[1],	R[1], 	W1, 		H1,			W2, 	H2, 	RW2H2);
	    __drawSlice ( ctx, skinImage, 	x + C[0] + C[1], 	y + R[0], 		 	C[2],	R[1], 	W1 + W2, 	H1,			W3, 	H2, 	RW3H2);
	    __drawSlice ( ctx, skinImage, 	x, 					y + R[0] + R[1], 	C[0],	R[2], 	0, 			H1 + H2, 	W1, 	H3, 	RW1H3);
	    __drawSlice ( ctx, skinImage, 	x + C[0], 			y + R[0] + R[1], 	C[1],	R[2], 	W1, 		H1 + H2, 	W2, 	H3, 	RW2H3);
	    __drawSlice ( ctx, skinImage, 	x + C[0] + C[1], 	y + R[0] + R[1], 	C[2],	R[2], 	W1 + W2, 	H1 + H2, 	W3, 	H3, 	RW3H3);

		ctx.restore();

		delete R;
		delete C;
	};

	/////////////////////////////////////////////////////////////////////////////////////
	private static function __drawSlice(ctx:CanvasRenderingContext2D, skin:CocoImage, cx:Integer, cy:Integer, cw:Integer, ch:Integer, sx:Integer, sy:Integer, sw:Integer, sh:Integer, r:COCO_SKIN_TILE_ENUM)
	{
	    if(cw==0 || ch==0 || sw==0 || sh==0) return;
	    if(r == COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE)
	        __drawRegion(ctx, skin, sx, sy, sw, sh, cx, cy, cw, ch);
	    else
	    	__tileSkin(ctx, skin, sx, sy, sw, sh, cx, cy, cw, ch);
	}

	/////////////////////////////////////////////////////////////////////////////////////
	private static function __countFixed(v:Array<Integer>, optional dispose:Boolean) :Integer
	{
	    var c:Integer = 0;
	    var L:Integer = v.size();
	    for(var i:Integer=0; i<L; i++)
	    	if(v[i]!=0) c++;
	    if(dispose) delete v;
	    return c;
	}

	/////////////////////////////////////////////////////////////////////////////////////
	private static function __fixedW(v:Array<COCO_SKIN_TILE_ENUM>, optional dispose:Boolean) :Boolean
	{
		var R:Boolean = false;
		var L:Integer = v.size();
	    for(var i:Integer=0; i<L; i++)
	        R |= ((v[i]==COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE) || (v[i]==COCO_SKIN_TILE_ENUM.TILE_REPEAT_Y));
	    if(dispose) delete v;
	    return R;
	}

	/////////////////////////////////////////////////////////////////////////////////////
	private static function __fixedH(v:Array<COCO_SKIN_TILE_ENUM>, optional dispose:Boolean) :Boolean
	{
		var R:Boolean = false;
		var L:Integer = v.size();
	    for(var i:Integer=0; i<L; i++)
	        R |= ((v[i]==COCO_SKIN_TILE_ENUM.TILE_REPEAT_NONE) || (v[i]==COCO_SKIN_TILE_ENUM.TILE_REPEAT_X));
	    if(dispose) delete v;
	    return R;
	}

	/////////////////////////////////////////////////////////////////////////////////////
	private static function __tileSkin(ctx:CanvasRenderingContext2D, image:CocoImage, cellX:Integer, cellY:Integer, cellWidth:Integer, cellHeight:Integer, x:Integer, y:Integer, width:Integer, height:Integer)
	{
		if(cellWidth==0 || cellHeight==0)
			return;

		var ix:Integer, iy:Integer, cw:Integer, ch:Integer;

		for(iy=y; iy<=y+height; iy+=cellHeight)
		{
			for(ix=x; ix<=x+width; ix+=cellWidth)
			{
				cw = (ix+cellWidth<=x+width ? cellWidth : x+width-ix);
				ch = (iy+cellHeight<=y+height ? cellHeight : y+height-iy);
				if(cw>0 && ch>0)
					__drawRegion(ctx, image, cellX, cellY, cw, ch, ix, iy, cw, ch);
			}
		}
	}

	/////////////////////////////////////////////////////////////////////////////////////
	private static function __drawRegion(ctx:CanvasRenderingContext2D, image:CocoImage, sx:Integer, sy:Integer, sw:Integer, sh:Integer, dx:Integer, dy:Integer, dw:Integer, dh:Integer)
	{
		if(!ctx || !image || sw==0 || sh==0 || dw==0 || dh==0)
			return;

		ctx.drawImage(image.hImage, sx+image.binpackX, sy+image.binpackY, sw, sh, dx, dy, dw, dh);
	}

	// ==================================================================================================================================
	//	    ____                        __                        __   ___               __               _
	//	   / __ )____  __  ______  ____/ /____   ____ _____  ____/ /  /   |  ____  _____/ /_  ____  _____(_)___  ____ _
	//	  / __  / __ \/ / / / __ \/ __  / ___/  / __ `/ __ \/ __  /  / /| | / __ \/ ___/ __ \/ __ \/ ___/ / __ \/ __ `/
	//	 / /_/ / /_/ / /_/ / / / / /_/ (__  )  / /_/ / / / / /_/ /  / ___ |/ / / / /__/ / / / /_/ / /  / / / / / /_/ /
	//	/_____/\____/\__,_/_/ /_/\__,_/____/   \__,_/_/ /_/\__,_/  /_/  |_/_/ /_/\___/_/ /_/\____/_/  /_/_/ /_/\__, /
	//	                                                                                                      /____/
	// ==================================================================================================================================

	public static function AlignToCocoHVAlign(h:COCO_TEXT_ALIGN_ENUM, v:COCO_TEXT_ALIGN_ENUM) :CocoHVAlign
	{
		var align:CocoHVAlign = new CocoHVAlign();

		switch(h)
		{
			case COCO_TEXT_ALIGN_ENUM.TEXT_ALIGN_NEAR:		align.horiz = 0; break;
			case COCO_TEXT_ALIGN_ENUM.TEXT_ALIGN_CENTER:	align.horiz = 0.5; break;
			case COCO_TEXT_ALIGN_ENUM.TEXT_ALIGN_FAR:		align.horiz = 1; break;
			default:break;
		}

		switch(v)
		{
			case COCO_TEXT_ALIGN_ENUM.TEXT_ALIGN_NEAR:		align.vert = 0; break;
			case COCO_TEXT_ALIGN_ENUM.TEXT_ALIGN_CENTER:	align.vert = 0.5; break;
			case COCO_TEXT_ALIGN_ENUM.TEXT_ALIGN_FAR:		align.vert = 1; break;
			default:break;
		}

		return align;
	}

	/////////////////////////////////////////////////////////////////////////////////////
	public static function AnchorToCocoHVAlign(anchor:COCO_CONTROL_ANCHOR_ENUM) :CocoHVAlign
	{
		var align:CocoHVAlign = new CocoHVAlign();

		align.horiz = 0.5;
		align.vert = 0.5;

    	switch(anchor)
    	{
		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_TOP_LEFT:
			align.vert = 0.0;
			align.horiz = 0.0;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_TOP_MIDDLE:
			align.vert = 0.0;
			align.horiz = 0.5;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_TOP_RIGHT:
			align.vert = 0.0;
			align.horiz = 1.0;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_MIDDLE_LEFT:
			align.vert = 0.5;
			align.horiz = 0.0;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_MIDDLE_RIGHT:
			align.vert = 0.5;
			align.horiz = 1.0;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_BOTTOM_LEFT:
			align.vert = 1.0;
			align.horiz = 0.0;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_BOTTOM_MIDDLE:
			align.vert = 1.0;
			align.horiz = 0.5;
			break;

		case COCO_CONTROL_ANCHOR_ENUM.ANCHOR_BOTTOM_RIGHT:
			align.vert = 1.0;
			align.horiz = 1.0;
			break;

		default:break;
    	}

    	return align;
	}
}
