"#export native";
"#export web";

/* ***** BEGIN LICENSE BLOCK *****
 *
 * Copyright (C) 2013-2014 www.coconut2D.org
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 * ***** END LICENSE BLOCK ***** */

// ==================================================================================================================================
//	   ______                 __  ___________                 _____    ___
//	  / ____/___  _________  / / / /  _/ ___/______________  / / / |  / (_)__ _      __
//	 / /   / __ \/ ___/ __ \/ / / // / \__ \/ ___/ ___/ __ \/ / /| | / / / _ \ | /| / /
//	/ /___/ /_/ / /__/ /_/ / /_/ // / ___/ / /__/ /  / /_/ / / / | |/ / /  __/ |/ |/ /
//	\____/\____/\___/\____/\____/___//____/\___/_/   \____/_/_/  |___/_/\___/|__/|__/
//
// ==================================================================================================================================

"#include CocoUIView.jspp";

class CocoUIScrollView : CocoUIView
{
	private var __scrollLastTime:Time;
	private var __scrollFirstPointY:Float;
	private var __scrollLastPointY:Float;
	private var __scrollSpeedY:Float;
	private var __minY:Float;
	private var __maxY:Float;
	private var __bounceY:Boolean;
	protected var __scrollContainer:CocoClip;
	private var __scrollDeceleration:Float;
	private var __scrollSpringTension:Float;

	////////////////////////////////////////////////////////////////////////////////////////////////////
 	public function Constructor(name:String, scene:CocoScene)
 	{
		__instanceName = name;
 		__scene = scene;
		__container = true;
		__scrollSpeedY = 0.0;
		__minY = 0
		__maxY = 0;
		__bounceY = false;
		__scrollDeceleration = 0.95;
		__scrollSpringTension = 16.0;

		// Add a container clip to place all children so that
		// during scrolling we only scroll this container.
 		__scrollContainer = new CocoClip();
 		__scrollContainer.__instanceName = "__scrollContainer";
 		__scrollContainer.__timeline.clear();
		__scrollContainer.__timeline.addKeyFrameEx(null, null, 0, COCO_KEYFRAME_INTERPOLATION_ENUM.KEYFRAME_INTERPOLATION_ECHO, false, true, 0, 0, 1, 1, 0, 0, 0, 0, 1, 1, 1, 1, null, false, false, '');
 		super.addChild(__scrollContainer);

 		// We need to be able to handle events
 		this.__timeline.keyFrame(0).handleEvents=true;
		this.addEventListener(this.OnTouchStart, __CocoUIScrollView_OnTouchStart);
 		this.addEventListener(this.OnTouchMove, __CocoUIScrollView_OnTouchMove);
 		this.addEventListener(this.OnTouchEnd, __CocoUIScrollView_OnTouchEnd);
 	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
 	public function Destructor()
 	{
 		this.removeEventListener(this.OnTouchStart, __CocoUIScrollView_OnTouchStart);
 		this.removeEventListener(this.OnTouchMove, __CocoUIScrollView_OnTouchMove);
 		this.removeEventListener(this.OnTouchEnd, __CocoUIScrollView_OnTouchEnd);
 	}

 	////////////////////////////////////////////////////////////////////////////////////////////////////
 	published property ScrollDeceleration
 	{
 		function get():Float { return __scrollDeceleration; }
 		function set(v:Float) { __scrollDeceleration = v; }
 	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	published property ScrollSpringTension
 	{
 		function get():Float { return __scrollSpringTension; }
 		function set(v:Float) { __scrollSpringTension = v; }
 	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function getKeyFrame() :CocoKeyFrame
	{
		return __scrollContainer.__timeline.keyFrame(0);
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function inlineHeight() :Float
	{
		var h:Float = 0;
		var ctl:CocoUIControl;
		for(var i:Integer=__scrollContainer.__children.size();i--;)
		{
			ctl = CocoUIControl(__scrollContainer.__children[i]);
			h+= ctl.MarginTop + ctl.Height + ctl.MarginBottom;
		}
		__maxY = Math.min(__minY, Height - h);
		__scrollContainer.__height = h;
		__scrollContainer.__width = __width;
		return h;
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function resetSelection()
	{
		var h:Float = 0;
		var ctl:CocoUIControl;
		for(var i:Integer=__scrollContainer.__children.size();i--;)
		{
			ctl = CocoUIControl(__scrollContainer.__children[i]);
			ctl.Selected = false;
		}
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
 	public virtual function paint(ctx:ICocoRenderContext, scene:CocoScene, parentClip:CocoClip, calcBoundingBox:Boolean, level:Integer)
 	{
 		var KF:CocoKeyFrame = getKeyFrame();

 		__scrollSpeedY *= __scrollDeceleration;
 		KF.y += __scrollSpeedY;
 		if(__bounceY)
 		{
	 		var springY:Float = __minY - KF.y;
	 		if(springY < 0.0)
	 		{
	 			__scrollSpeedY *= __scrollDeceleration;
				KF.y += -springY / -Math.sqrt(-springY);
				if(KF.y < __minY)
				{
					KF.y = __minY;
					__bounceY = false;
					__scrollSpeedY = 0.0;
				}
	 		}
	 		else
	 		{
		 		var springY:Float = __maxY - KF.y;
		 		if(springY > 0.0)
		 		{
		 			__scrollSpeedY *= __scrollDeceleration;
		 			KF.y += springY / Math.sqrt(springY);
		 			if(KF.y > __maxY)
		 			{
		 				KF.y = __maxY;
		 				__bounceY = false;
		 				__scrollSpeedY = 0.0;
		 			}
		 		}
	 		}
 		}

 		super.paint;
 	}

 	////////////////////////////////////////////////////////////////////////////////////////////////////
 	public function __CocoUIScrollView_OnTouchStart(sender:CocoEventSource, e:CocoEvent, x:Float, y:Float) :Boolean
	{
		__scrollFirstPointY = y;
		__scrollLastTime = engine.__clock;
		__scrollLastPointY = y;
		__scrollSpeedY = 0.0;
		return false;
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function __CocoUIScrollView_OnTouchMove(sender:CocoEventSource, e:CocoEvent, x:Float, y:Float) :Boolean
	{
		var KF:CocoKeyFrame = getKeyFrame();

		var dY:Float = y - __scrollLastPointY;
		var sY:Float = 1.0;
 		var springY:Float = __minY - KF.y;
 		if(springY < 0.0)
 		{
			sY = Height / (Height - springY * __scrollSpringTension);
 		}
 		else
 		{
	 		var springY:Float = __maxY - KF.y;
	 		if(springY > 0.0)
	 		{
	 			sY = Height / (Height + springY * __scrollSpringTension);
	 		}
 		}

		if(Math.abs(__scrollFirstPointY-y)>COCO_CONTROL_MOVE_SNAP_PIXELS)
 			resetSelection();

		KF.y += dY * sY;
		__scrollLastTime = engine.__clock;
		__scrollLastPointY = y;
		return false;
	}

	////////////////////////////////////////////////////////////////////////////////////////////////////
	public function __CocoUIScrollView_OnTouchEnd(sender:CocoEventSource, e:CocoEvent, x:Float, y:Float) :Boolean
	{
		var dY:Float = y - __scrollLastPointY;
		__scrollSpeedY = dY;
		__bounceY = true;
		resetSelection();
		return false;
	}
}
